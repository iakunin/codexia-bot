plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'io.freefair.lombok' version '5.1.0'
    id 'org.sonarqube' version '2.8'
    id 'jacoco'
}

group = 'dev.iakunin'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    dataDog
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core:3.9.0'
    implementation 'com.vladmihalcea:hibernate-types-52:2.9.11'
    implementation 'com.fasterxml.jackson.module:jackson-modules-java8:2.11.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.11.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.+'
    implementation 'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.11.0'

    implementation 'de.siegmar:logback-gelf:3.0.0'
    implementation 'org.cactoos:cactoos:0.46'
    implementation 'io.vavr:vavr:0.10.3'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:2.2.3.RELEASE'
    implementation 'io.github.openfeign:feign-jackson:11.0'

    implementation 'org.kohsuke:github-api:1.112'
    implementation 'net.dean.jraw:JRAW:1.1.0'

    implementation 'io.sentry:sentry-logback:1.7.30'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation group: 'com.github.database-rider', name: 'rider-spring', version: '1.13.1', {
        exclude group: 'org.slf4j'
    }
    testImplementation 'org.testcontainers:postgresql:1.14.3'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
    testImplementation 'com.github.tomakehurst:wiremock:2.26.3'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.4'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.6.2'

    dataDog 'com.datadoghq:dd-java-agent:0.54.0'
}

springBoot {
    buildInfo {
        properties {
            version = version
        }
    }
}

test {
    useJUnitPlatform()
    failFast = true
    maxParallelForks = 2
}

lombok {
    version = '1.18.10'
    config['lombok.accessors.chain'] = 'true'
    config['lombok.equalsAndHashCode.callSuper'] = 'skip'
    config['lombok.toString.callSuper'] = 'call'
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'iakunin_codexia-bot'
        property 'sonar.organization', 'iakunin'
        property 'sonar.host.url', 'https://sonarcloud.io'
        if (project.hasProperty('pullRequestKey') && project.property('pullRequestKey')) {
            property 'sonar.pullrequest.key', project.property('pullRequestKey')
        }
        if (!System.getenv('SONAR_TOKEN')) {
            sonarqube.skipProject = true
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test.finalizedBy jacocoTestReport

task copyDataDog(type: Copy) {
    from configurations.dataDog
    into "${project.buildDir}/libs"
}

assemble.dependsOn copyDataDog
