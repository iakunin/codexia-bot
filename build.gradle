plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'io.freefair.lombok' version '4.1.6'
    id 'org.sonarqube' version '2.8'
    id 'jacoco'
}

group = 'dev.iakunin'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    implementation 'org.postgresql:postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.liquibase:liquibase-core'
    implementation 'com.vladmihalcea:hibernate-types-52:2.7.0'
    implementation 'com.fasterxml.jackson.module:jackson-modules-java8:2.10.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.10.1'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.+'

    implementation 'de.siegmar:logback-gelf:2.0.1'
    implementation 'org.cactoos:cactoos:0.43'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:2.1.1.RELEASE'
    implementation 'io.github.openfeign:feign-jackson:9.3.1'

    implementation 'org.kohsuke:github-api:1.106'
    implementation 'net.dean.jraw:JRAW:1.1.0'


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation group: 'com.github.database-rider', name: 'rider-spring', version: '1.10.0', {
        exclude group: 'org.slf4j'
    }
    testImplementation 'org.testcontainers:postgresql:1.13.0'
    testImplementation 'com.github.javafaker:javafaker:1.0.2'
    testImplementation 'com.github.tomakehurst:wiremock:2.26.3'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.2'
}

springBoot {
    buildInfo {
        properties {
            version = version
        }
    }
}

test {
    useJUnitPlatform()
    failFast = true
}

lombok {
    version = '1.18.10'
    config['lombok.accessors.chain'] = 'true'
    config['lombok.equalsAndHashCode.callSuper'] = 'skip'
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'iakunin_codexia-bot'
        property 'sonar.organization', 'iakunin'
        property 'sonar.host.url', 'https://sonarcloud.io'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

test.finalizedBy jacocoTestReport
